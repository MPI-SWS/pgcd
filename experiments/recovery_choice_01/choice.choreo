Choice =
def x0 = { True } f1 || { True } cc1
    f1 = (franka: idle(), cart: idle(), arm: idle()); f2
    cc1 = (crane: moveObject(180, 0, 110, 0, 160, 175), carrier: idle()); cc2
    cc2 = crane -> carrier: Ok(); cc3
    cc3 = (crane: idle(), carrier: moveCart(0, 0, 0, 800)); cc4
    cc4 || f2 = x2
    x2 = carrier -> crane: Ok(); x3                             # synchonize all the processes for checkpoint
    x3 = carrier -> franka: Ok(); x4
    x4 = carrier -> cart: Ok(); x5
    x5 = carrier -> arm: Ok(); x6
    x6 = checkpoint(1); choice
    choice = [True] to_arm + [True] to_franka

    to_arm = carrier -> arm: Ok(); to_arm_0                     # drop at cart and arm
    to_arm_0 = carrier -> franka: Done(); to_arm_1
    to_arm_1 = {True} crf0 || {True} cca0                       # wait until cart get in position
    crf0 = (crane: idle(), franka: idle()); crf1
    cca0 = {True} ca0 || {True} c0
    ca0 = (cart: idle(), arm: idle()); ca1
    c0 = (carrier: rotate(800, 0, 0, 90)); c1
    c1 = (carrier: moveCart(800, 0, 90, 550)); c2
    ca1 || c2 = cca1
    cca1 = carrier -> arm: Ok(); cca2                           # arm grab object
    cca2 = {True} a0 || {True} ccc0
    a0 = (arm: rotate(0,0,0,0,270,190)); a1
    a1 = (arm: closeGripper()); a2
    a2 = (arm: rotate(0,270,190,0,0,0)); a3
    ccc0 = (carrier: idle(), cart: idle()); ccc1
    a3 || ccc1 = cca3
    cca3 = arm -> cart: Ok(); cca4                              # cart move to drop-off point, carrier goes back
    cca4 = arm -> carrier: Ok(); cca5
    cca5 = {True} c3 || {True} ca2
    c3 = (carrier: moveCart(800, 550, 90, -550)); c4
    c4 = (carrier: rotate(800, 0, 90, -85)); c5
    c5 = (carrier: moveCart(800, 0, 0, -800)); c6
    c6 = (carrier: idle()); c7
    ca2 = (arm: idle(), cart: moveCart(0, 0, 0, -150)); ca3
    ca3 = cart -> arm: Ok(); ca4                                # arm drop object
    ca4 = {True} crt0 || {True} a4
    crt0 = (cart: idle()); crt1
    a4 = (arm: rotate(0,0,0,90,270,190)); a5
    a5 = (arm: openGripper()); a6
    a6 = (arm: rotate(90,270,190,0,0,0)); a7
    crt1 || a7 = ca5
    ca5 = arm -> cart: Ok(); ca6                                # cart move back
    ca6 = {True} a8 || {True} crt2
    a8 = (arm: idle()); a9
    crt2 = (cart: moveCart(-150, 0, 0,  150)); crt3
    crt3 || a9 = ca7
    ca7 || c7 = cca6
    cca6 = cart -> carrier: Ok(); cca7
    cca7 || crf1 = to_arm_2

    to_franka = carrier -> arm: Done(); to_franka_0
    to_franka_0 = arm -> cart: Done(); to_franka_1
    to_franka_1 = {True} cac0 || {True} cf0
    cac0 = (crane: idle(), arm: idle(), cart: idle()); cac1
    cf0 = (carrier: moveCart(800, 0, 0, 700), franka: idle()); cf1                  # carrier move to franka
    cf1 = carrier -> franka: Ok(); cf2                                              # franka grab
    cf2 = {True} cfc0 || {True} cff0
    cfc0 = (carrier: idle()); cfc1
    cff0 = (franka: setJoints( 0.000000, -0.785398, -0.000000, -2.356195, 0.000000, 1.570796, 0.785398, 0.137950, 0.383110, 0.019275, -1.932828, 0.016947, 2.304120, 0.936556)); cff1
    cff1 = (franka: grasp(0.03)); cff2
    cff2 = (franka: homePos(0.137950, 0.383110, 0.019275, -1.932828, 0.016947, 2.304120, 0.936556)); cff3
    cff3 || cfc1 = cf3
    cf3 = franka -> carrier: Ok(); cf4                                              # grabbed
    cf4 = {True} cfc2 || {True} cff4
    cfc2 = (carrier: moveCart(1500, 0, 0, -1500)); cfc3                             # carrier goes back
    cff4 = (franka: setJoints( 0.000000, -0.785398, -0.000000, -2.356195, 0.000000, 1.570796, 0.785398, 0.960601, 0.101824, -0.042593, -2.81301, 0.031016, 2.885839, 1.677218) ); cff5    # franka drop object
    cff5 = (franka: openGripper(0.03)); cff6
    cff6 = (franka: homePos(0.960601, 0.101824, -0.042593, -2.81301, 0.031016, 2.885839, 1.677218)); cff7
    cff7 = (franka: idle()); cff8
    cfc3 || cff8 = cf5
    cf5 || cac1 = to_franka_2

    to_arm_2 + to_franka_2 = x7                                                     # sync ending
    x7 = carrier -> crane: Ok(); x8
    x8 = carrier -> franka: Ok(); x9
    x9 = carrier -> arm: Ok(); x10
    x10 = carrier -> cart: Ok(); x11
    x11 = end
in [ True ] x0
